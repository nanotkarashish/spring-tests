buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath(
                'io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE',
                'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.2.0',
                'org.akhikhl.gretty:gretty:+'
        )
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.unbroken-dome.test-sets'
apply plugin: 'org.akhikhl.gretty'

//If you are using Gradle 2.1+, you can use the new Plugins DSL. The
//following snippet replaces every line above this comment:
/*plugins {
    id 'java'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
}*/


testSets {
    integrationTest { dirName = 'integration-test' }
    endToEndTest { dirName = 'end-to-end-test' }
}

check.dependsOn integrationTest
check.dependsOn endToEndTest
endToEndTest.mustRunAfter integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

gretty {
    httpPort = 8080
    contextPath = '/'
    servletContainer = 'jetty9.3'
    integrationTestTask = 'endToEndTest'
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:Athens-SR3'
    }
}

dependencies {
    compile(
            'org.slf4j:slf4j-api',
            'org.springframework:spring-aspects',
            'org.springframework:spring-context-support',
            'org.springframework:spring-webmvc',
    )
    providedCompile(
            'javax.servlet:javax.servlet-api'
    )
    runtime(
            'javax.servlet:jstl',
            'log4j:log4j',
            'org.slf4j:slf4j-log4j12',
            'org.slf4j:jcl-over-slf4j',
    )
    testCompile(
            'javax.el:javax.el-api',
            'org.glassfish.web:el-impl:2.2',
            'junit:junit',
            'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1',
            'org.hamcrest:hamcrest-library',
            'org.assertj:assertj-core:3.5.2',
            'org.springframework:spring-test'
    )
    endToEndTestCompile(
            'org.seleniumhq.selenium:selenium-java:3.1.0'
    )
}

test {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

integrationTest {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.IntegrationTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

endToEndTest {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.EndToEndTest'
    }
    testLogging {
        showStandardStreams = true
    }
}