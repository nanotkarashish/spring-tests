buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath(
                'io.spring.gradle:dependency-management-plugin:1.0.0.RELEASE',
                'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.2',
                'org.akhikhl.gretty:gretty:2.0.0'
        )
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.unbroken-dome.test-sets'
apply plugin: 'org.akhikhl.gretty'

//If you are using Gradle 2.1+, you can use the new Plugins DSL. The
//following snippet replaces every line above this comment:
/*plugins {
    id 'java'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
}*/


testSets {
    integrationTest { dirName = 'integration-test' }
    endToEndTest { dirName = 'end-to-end-test' }
}

check.dependsOn integrationTest
check.dependsOn endToEndTest
endToEndTest.mustRunAfter integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

ant.properties['project.basedir'] = project.rootDir
ant.importBuild 'src/main/ant/build.xml'
compileJava.finalizedBy(dist)

gretty {
    httpPort = 8080
    contextPath = '/'
    extraResourceBase 'frontend/build'
    servletContainer = 'jetty9.4'

    if (gradle.startParameter.taskNames.contains('endToEndTest')) {
        systemProperty 'spring.profiles.active', 'endToEndTest'
    }
    else {
        systemProperty 'spring.profiles.active', 'application'
    }

    integrationTestTask = 'endToEndTest'
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'io.spring.platform:platform-bom:Brussels-SR4'
    }
}

dependencies {
    compile(
            'org.apache.commons:commons-lang3',
            'org.slf4j:slf4j-api',
            'log4j:log4j',
            'org.slf4j:slf4j-log4j12',
            'org.slf4j:jcl-over-slf4j',
            'org.springframework:spring-aspects',
            'org.springframework:spring-context-support',
            'org.springframework:spring-jdbc',
            'javax.servlet:javax.servlet-api',
            'javax.servlet:jstl',
            'org.hibernate:hibernate-validator',
            'com.fasterxml.jackson.core:jackson-annotations',
            'com.fasterxml.jackson.core:jackson-core',
            'com.fasterxml.jackson.core:jackson-databind',
            'com.fasterxml.jackson.datatype:jackson-datatype-jdk8',
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
            'org.springframework:spring-webmvc',
            'org.springframework.security:spring-security-core',
            'org.springframework.security:spring-security-config',
            'org.springframework.security:spring-security-web',
            'com.h2database:h2',
            'com.zaxxer:HikariCP',
            'org.hibernate:hibernate-core:5.2.10.Final',
            'org.hibernate:hibernate-entitymanager:5.2.10.Final',
            'org.springframework.data:spring-data-jpa',
            'org.liquibase:liquibase-core'
    )
    testCompile(
            'javax.el:javax.el-api',
            'org.glassfish.web:el-impl:2.2',
            'junit:junit',
            'org.hamcrest:hamcrest-library',
            'org.assertj:assertj-core:3.5.2',
            'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1',
            'org.mockito:mockito-core',
            'info.solidsoft.mockito:mockito-java8:0.3.1',
            'com.jayway.jsonpath:json-path',
            'org.springframework:spring-test',
    )
    integrationTestCompile sourceSets.test.output
    integrationTestCompile(
            'org.dbunit:dbunit:2.5.3',
            'com.github.springtestdbunit:spring-test-dbunit:1.3.0',
            'org.springframework.security:spring-security-test'
    )
    endToEndTestCompile(
            'com.google.guava:guava:21.0',
            'org.seleniumhq.selenium:selenium-java:3.3.1',
            'org.seleniumhq.selenium:selenium-support:3.3.1'
    )
    // We need to declare this dependency or otherwise we get the following error when we start Jetty:
    // Provider ch.qos.logback.classic.servlet.LogbackServletContainerInitializer not found
    runtime(
            'ch.qos.logback:logback-classic:1.2.3'
    )
}

test {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

integrationTest {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.IntegrationTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

endToEndTest {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.EndToEndTest'
    }
    testLogging {
        showStandardStreams = true
    }
    systemProperty 'webdriver.base.url', 'http://localhost:8080/'
    systemProperty 'webdriver.html5.mode.enabled', 'false'
}