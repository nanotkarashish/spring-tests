buildscript {
    repositories{
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(
                'org.springframework.boot:spring-boot-gradle-plugin:1.5.7.RELEASE',
                'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.2'
        )
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.unbroken-dome.test-sets'

//If you are using Gradle 2.1+, you can use the new Plugins DSL. The
//following snippet replaces every line above this comment:
/*plugins {
    id 'java'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
}*/


testSets {
    integrationTest { dirName = 'integration-test' }
    endToEndTest { dirName = 'end-to-end-test' }
}

check.dependsOn integrationTest
check.dependsOn endToEndTest
endToEndTest.mustRunAfter integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

bootRun {
    systemProperty('spring.profiles.active', 'application')
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'org.apache.commons:commons-lang3:3.5',
            'org.springframework.boot:spring-boot-starter-jdbc',
            'com.h2database:h2',
            'com.zaxxer:HikariCP',
            'org.liquibase:liquibase-core',
            'org.hibernate:hibernate-java8',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-security',
            'org.springframework.boot:spring-boot-starter-thymeleaf',
            'org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.2.RELEASE',
            'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.0.RELEASE'
    )
    testCompile(
            'org.springframework.boot:spring-boot-starter-test',
            'org.assertj:assertj-core:3.7.0',
            'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1',
            'info.solidsoft.mockito:mockito-java8:0.3.1'
    )
    integrationTestCompile sourceSets.test.output
    integrationTestCompile(
            'org.dbunit:dbunit:2.5.3',
            'com.github.springtestdbunit:spring-test-dbunit:1.3.0',
            'org.springframework.security:spring-security-test'
    )
    endToEndTestCompile(
            'com.google.guava:guava:21.0',
            'org.seleniumhq.selenium:selenium-java:3.3.1',
            'org.seleniumhq.selenium:selenium-support:3.3.1'
    )
}

//Use Thymeleaf 3
ext["thymeleaf.version"] = "3.0.6.RELEASE"
ext["thymeleaf-layout-dialect.version"] = "2.2.1"

test {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

integrationTest {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.IntegrationTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

endToEndTest {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.EndToEndTest'
    }
    testLogging {
        showStandardStreams = true
    }
    systemProperty 'webdriver.base.url', 'http://localhost:8080/'
}