buildscript {
    repositories{
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(
                'org.springframework.boot:spring-boot-gradle-plugin:1.5.1.RELEASE',
                'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.2.0'
        )
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.unbroken-dome.test-sets'

//If you are using Gradle 2.1+, you can use the new Plugins DSL. The
//following snippet replaces every line above this comment:
/*plugins {
    id 'java'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
}*/


testSets {
    integrationTest { dirName = 'integration-test' }
    endToEndTest { dirName = 'end-to-end-test' }
}

check.dependsOn integrationTest
check.dependsOn endToEndTest
endToEndTest.mustRunAfter integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

bootRun {
    systemProperty('spring.profiles.active', 'application')
}

repositories {
    mavenCentral()
}


dependencies {
    compile(
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-thymeleaf'
    )
    testCompile(
            'org.springframework.boot:spring-boot-starter-test',
            'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1'
    )
    integrationTestCompile(
            'org.dbunit:dbunit:2.5.3',
            'com.github.springtestdbunit:spring-test-dbunit:1.3.0'
    )
    integrationTestRuntime(
            //Add dependencies that are required to run
            //our integration tests.
    )
    endToEndTestCompile(
            'org.seleniumhq.selenium:selenium-java:3.3.1'
    )
    endToEndTestRuntime(
            //Add dependencies that are required to run our
            //end-to-end tests
    )
}

test {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

integrationTest {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.IntegrationTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

endToEndTest {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.EndToEndTest'
    }
    testLogging {
        showStandardStreams = true
    }
}