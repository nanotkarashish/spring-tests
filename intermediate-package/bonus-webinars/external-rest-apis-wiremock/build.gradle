buildscript {
    repositories{
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(
                'org.springframework.boot:spring-boot-gradle-plugin:1.5.3.RELEASE',
                'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.2.0'
        )
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.unbroken-dome.test-sets'

//If you are using Gradle 2.1+, you can use the new Plugins DSL. The
//following snippet replaces every line above this comment:
/*plugins {
    id 'java'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
}*/


testSets {
    integrationTest { dirName = 'integration-test' }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

bootRun {
    systemProperty('spring.profiles.active', 'application')
}

repositories {
    mavenCentral()
}


dependencies {
    compile(
            'org.springframework.boot:spring-boot-starter-web'
    )
    testCompile(
            'org.assertj:assertj-core:3.6.2',
            'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1',
            'org.springframework.boot:spring-boot-starter-test'
    )
    integrationTestCompile sourceSets.test.output
    integrationTestCompile (
            'com.github.tomakehurst:wiremock:2.6.0'
    )
    integrationTestRuntime(
            //Add dependencies that are required to run
            //our integration tests.
    )
}

test {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

integrationTest {
    useJUnit {
        includeCategories 'com.testwithspring.intermediate.IntegrationTest'
    }
    testLogging {
        showStandardStreams = true
    }
}