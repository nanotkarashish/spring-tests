buildscript {
    repositories{
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(
                'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.2',
                'org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE'
        )
    }
}

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.unbroken-dome.test-sets'

testSets {
    endToEndTest { dirName = 'end-to-end-test' }
    integrationTest { dirName = 'integration-test' }
}

check.dependsOn integrationTest
check.dependsOn endToEndTest
endToEndTest.mustRunAfter integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

bootRun {
    systemProperty('spring.profiles.active', 'application')
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'org.apache.commons:commons-lang3:3.5',
            'org.springframework.boot:spring-boot-starter-jdbc',
            'com.h2database:h2',
            'com.zaxxer:HikariCP',
            'org.liquibase:liquibase-core',
            'org.hibernate:hibernate-java8',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-security',
            'org.springframework.boot:spring-boot-starter-thymeleaf',
            'org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.2.RELEASE',
            'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.0.RELEASE'
    )
    testCompile(
            'org.springframework.boot:spring-boot-starter-test',
            'org.codehaus.groovy:groovy-all',
            'org.spockframework:spock-core'
    )
    testRuntime(
            'net.bytebuddy:byte-buddy:1.7.1'
    )
    testRuntime('com.athaydes:spock-reports:1.3.1') {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
    integrationTestCompile sourceSets.test.output
    integrationTestCompile(
            'org.springframework.security:spring-security-test',
            'org.dbunit:dbunit:2.5.4',
            'com.github.springtestdbunit:spring-test-dbunit:1.3.0'
    )
    integrationTestRuntime(
            'org.spockframework:spock-spring'
    )
    endToEndTestCompile(
            'org.seleniumhq.selenium:selenium-java'
    )
    endToEndTestRuntime configurations.integrationTestRuntime
}

test {
    useJUnit {
        includeCategories 'com.testwithspring.master.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

integrationTest {
    useJUnit {
        includeCategories 'com.testwithspring.master.IntegrationTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

endToEndTest {
    useJUnit {
        includeCategories 'com.testwithspring.master.EndToEndTest'
    }
    testLogging {
        showStandardStreams = true
    }
    systemProperty 'webdriver.base.url', 'http://localhost:8080/'
}