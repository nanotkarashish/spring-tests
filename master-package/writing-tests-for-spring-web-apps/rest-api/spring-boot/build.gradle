buildscript {
    repositories{
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath(
                'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.4.2',
                'org.springframework.boot:spring-boot-gradle-plugin:1.5.7.RELEASE'
        )
    }
}

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.unbroken-dome.test-sets'

testSets {
    integrationTest { dirName = 'integration-test' }
    endToEndTest { dirName = 'end-to-end-test' }
}

check.dependsOn integrationTest
check.dependsOn endToEndTest
endToEndTest.mustRunAfter integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

bootRun {
    systemProperty('spring.profiles.active', 'application')
}

repositories {
    mavenCentral()
}

dependencies {
    compile(
            'org.apache.commons:commons-lang3:3.5',
            'org.springframework.boot:spring-boot-starter-jdbc',
            'com.h2database:h2',
            'com.zaxxer:HikariCP',
            'org.liquibase:liquibase-core',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-security',
            'com.fasterxml.jackson.datatype:jackson-datatype-jdk8',
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310',
            'org.springframework.boot:spring-boot-starter-thymeleaf'
    )
    testCompile(
            'org.springframework.boot:spring-boot-starter-test',
            'org.codehaus.groovy:groovy-all',
            'org.spockframework:spock-core',
            'net.bytebuddy:byte-buddy:1.7.1'
    )
    testCompile('com.athaydes:spock-reports:1.3.1') {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }
    integrationTestCompile sourceSets.test.output
    integrationTestCompile(
            'org.spockframework:spock-spring',
            'org.springframework.security:spring-security-test',
            'org.dbunit:dbunit:2.5.4',
            'com.github.springtestdbunit:spring-test-dbunit:1.3.0'
    )
}

test {
    useJUnit {
        includeCategories 'com.testwithspring.master.UnitTest'
    }
    testLogging {
        showStandardStreams = true
    }
}

integrationTest {
    useJUnit {
        includeCategories 'com.testwithspring.master.IntegrationTest'
    }
    testLogging {
        showStandardStreams = true
    }
}